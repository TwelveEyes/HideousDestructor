//-------------------------------------------------
// Medikit
//-------------------------------------------------
const HD_MEDPATCHLIMIT=42;
class PortableMedikit:HDPickup{
	default{
		//$Category "Items/Hideous Destructor/Supplies"
		//$Title "Medikit"
		//$Sprite "PMEDA0"

		-hdpickup.droptranslation
		inventory.pickupmessage "Picked up a medikit.";
		inventory.icon "PMEDA0";
		scale 0.4;
		hdpickup.bulk ENC_MEDIKIT;
		hdpickup.nicename "Medikit";
		hdpickup.refid HDLD_MEDIKIT;
		species "HealingItem";
	}
	states{
	spawn:
		MEDI A -1;
		stop;
	use:
		TNT1 A 0{
			if(!FindInventory("HDMedikitter")){
				A_GiveInventory("HDMedikitter");
				if(A_JumpIfInventory("PortableStimpack",0,"null"))A_DropItem("PortableStimpack");
				else A_GiveInventory("PortableStimpack");
				let sb=hdweapon(spawn("SecondBlood",pos));
				if(sb)sb.actualpickup(self);
				A_TakeInventory("PortableMedikit",1);
			}else{
				A_Log("You pull out the medikit you've already unwrapped.",true);
			}
			A_SelectWeapon("HDMedikitter");
			A_PlaySound("weapons/pocket",CHAN_WEAPON);
		}
		fail;
	}
}

class HDWoundFixer:HDWeapon{
	default{
		+weapon.wimpy_weapon +weapon.no_auto_switch +weapon.cheatnotweapon
		hdwoundfixer.injectoricon "TNT1A0";
	}
	int checkwoundcount(bool checkunstable=false){
		let onr=HDPlayerPawn(owner);
		if(onr){
			if(checkunstable)return onr.woundcount+onr.unstablewoundcount;
			else return onr.woundcount;
		}
		return 0;
	}
	static bool DropMeds(actor caller,int amt=1){
		if(!caller)return false;
		array<inventory> items;items.clear();
		for(inventory item=caller.inv;item!=null;item=!item?null:item.inv){
			if(
				item.species=="HealingItem"
			){
				items.push(item);
			}
		}
		if(!items.size())return false;
		double aang=caller.angle;
		double ch=items.size()?20.:0;
		caller.angle-=ch*(items.size()-1)*0.5;
		for(int i=0;i<items.size();i++){
			caller.a_dropinventory(items[i].getclassname(),amt>0?amt:items[i].amount);
			caller.angle+=ch;
		}
		caller.angle=aang;
		return true;
	}
	string injectoricon;property injectoricon:injectoricon;
	class<inventory> injectortype;property injectortype:injectortype;
	override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		sb.drawimage(
			injectoricon,(-23,-7),
			sb.DI_SCREEN_CENTER_BOTTOM|sb.DI_ITEM_RIGHT
		);
		sb.drawnum(hpl.countinv(injectortype),-22,-8,sb.DI_SCREEN_CENTER_BOTTOM,font.CR_BLACK);
	}
	override void DropOneAmmo(int amt){
		DropMeds(owner,clamp(amt,1,10));
	}
	states{
	reload:
		TNT1 A 14{
			A_DropInventory("HDArmourWorn");
			A_ClearRefire();
		}
		goto readyend;
	}
}
class HDMedikitter:HDWoundFixer{
	default{
		-weapon.no_auto_switch
		+inventory.invbar
		weapon.selectionorder 1001;
		weapon.slotnumber 9;
		scale 0.3;
	}
	enum MediNums{
		MEDIKIT_MAXFLESH=2,
		MEDIKIT_FLESHGIVE=200,
	}
	int injectableflesh;
	int usedpatches;
	actor usedon;
	int scanaccuracy;
	actor scantarget;
	override void beginplay(){
		injectableflesh=MEDIKIT_MAXFLESH;
		usedpatches=0;
		usedon=null;
		super.beginplay();
	}
	override double weaponbulk(){
		return ENC_MEDIKIT;
	}
	override string,double getpickupsprite(){
		return (!!usedon)?"MEDIC0":"MEDIB0",0.6;
	}
	override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		let ww=hdmedikitter(hdw);
		int of=0;
		if(
			hpl.woundcount
			&&(!ww.usedon||ww.usedon==hpl)
		){
			of=clamp(hpl.woundcount*0.1,1,3);
			if(hpl.flip)of=-of;
		}
		sb.drawwepdot(-27,-11+of,(2,6));
		sb.drawwepdot(-25,-13+of,(6,2));
		if(ww.usedon)sb.drawimage(
			"BLUDC0",(-14,-7),
			sb.DI_SCREEN_CENTER_BOTTOM|sb.DI_ITEM_RIGHT,
			0.8
		);
		int btn=hpl.player.cmd.buttons;
		if(
			!(btn&BT_FIREMODE)
			||(btn&BT_ZOOM)
		){
			sb.drawwepnum(HD_MEDPATCHLIMIT-ww.usedpatches,HD_MEDPATCHLIMIT);
			bool secondfleshing=(
				hpl.flip
				&&(btn&BT_ZOOM)
				&&(btn&BT_FIREMODE)
			);
			if(ww.injectableflesh==2)sb.drawwepdot(-20,-10-secondfleshing,(3,3));
			if(ww.injectableflesh>=1)sb.drawwepdot(-16,-10-secondfleshing,(3,3));
			sb.drawnum(hpl.countinv("PortableMedikit"),-43,-8,sb.DI_SCREEN_CENTER_BOTTOM,font.CR_BLACK);
		}
	}
	override string gethelptext(){
		return
		"\cuMedikit\n"
		..WEPHELP_RELOAD.."  Take off armour\n"
		..WEPHELP_INJECTOR
		.."\n  ...while pressing:\n"
		.."  <\cunothing"..WEPHELP_RGCOL..">  Staple and patch\n"
		.."  "..WEPHELP_FIREMODE.."  Run diagnostic\n"
		.."  "..WEPHELP_FIREMODE.."+"..WEPHELP_ZOOM.."  Apply Second Flesh"
		;
	}
	void patchwound(int amt,actor targ){
		let slf=HDPlayerPawn(targ);
		if(!slf)return;
		if(!random(0,1)&&(slf.alpha<1||slf.bshadow))amt-=random(0,amt+1);
		int wound=max(slf.woundcount,0);
		int unstablewound=max(slf.unstablewoundcount,0);
		if(wound){
			slf.woundcount=max(0,wound-amt);
		}else if(unstablewound){
			slf.unstablewoundcount=max(0,unstablewound-amt);
		}else amt=0;
		slf.oldwoundcount+=amt;
	}
	action void A_MedikitReady(){
		A_WeaponReady(WRF_NOFIRE|WRF_ALLOWUSER1|WRF_ALLOWUSER3);
		if(!player)return;
		int bt=player.cmd.buttons;

		//take off armour
		if(bt&BT_RELOAD){
			A_DropInventory("HDArmourWorn");
			setweaponstate("nope");
			return;
		}

		//wait for the player to decide what they're doing
		if(bt&BT_ATTACK&&bt&BT_ALTATTACK)return;

		//use on someone else
		if(bt&BT_ALTATTACK){
			if(invoker.usedpatches>HD_MEDPATCHLIMIT){
				A_WeaponMessage("You are out of Auto-Sutures.");
				setweaponstate("nope");
			}else setweaponstate("fireother");
			return;
		}

		//self
		if(bt&BT_ATTACK){
			//radsuit blocks everything
			if(countinv("WornRadsuit")){
				if(!countinv("PortableRadsuit"))A_TakeInventory("WornRadsuit");
				else{
					if(getcvar("hd_autostrip"))setweaponstate("reload");
					else{
						if(getcvar("hd_helptext"))A_WeaponMessage("Take off your environment suit first!\n\n(toggle it in your inventory or hit reload)",100);
						setweaponstate("nope");
					}
					return;
				}
			}
			if(pitch<80){
				//move downwards
				let hdp=hdplayerpawn(self);
				if(hdp)hdp.gunbraced=false;
				A_MuzzleClimb(0,5,0,5);
			}else{
				bool scanning=bt&BT_FIREMODE&&!(bt&BT_ZOOM);
				//armour blocks everything except scan
				if(
					!scanning
					&&countinv("HDArmourWorn")
				){
					if(getcvar("hd_autostrip"))setweaponstate("reload");
					else{
						if(getcvar("hd_helptext"))A_WeaponMessage("Take off your armour first!\n\n(\cdhd_strip\c- in the console\n\nor hit reload)",100);
						A_Refire("nope");
					}
					return;
				}
				//diagnose
				if(scanning){
					setweaponstate("diagnose");
					return;
				}
				//act upon flesh
				if(
					bt&BT_FIREMODE
					&&bt&BT_ZOOM
				){
					//second flesh
					if(invoker.injectableflesh<1){
						A_WeaponMessage("You are out of Second Flesh.");
						setweaponstate("nope");
						return;
					}
					let a=HDPlayerPawn(self);
					if(a){
						if(a.oldwoundcount+a.burncount+a.unstablewoundcount<1){
							A_WeaponMessage("You have no stabilized injuries to treat.");
							setweaponstate("nope");
						}else setweaponstate("secondflesh");
						return;
					}
				}else{
					//patch
					if(invoker.usedpatches>HD_MEDPATCHLIMIT){
						A_WeaponMessage("You are out of Auto-Sutures.");
						setweaponstate("nope");
					}else if(!invoker.checkwoundcount(true)){
						A_WeaponMessage("You are not bleeding.");
						setweaponstate("nope");
					}else setweaponstate("patchup");
					return;
				}
			}
		}
	}
	states{
	select:
		TNT1 A 0{
			if(invoker.injectableflesh<1 && invoker.usedpatches>HD_MEDPATCHLIMIT) A_SelectWeapon("SelfBandage");
		}
		TNT1 A 10{
			if(!getcvar("hd_helptext")) return;
			A_WeaponMessage("\cg+++ \cjMEDIKIT \cg+++\c-\n\n\nPress and hold Fire\nto patch yourself up.",175);
		}
		goto super::select;
	ready:
		TNT1 A 1 A_MedikitReady();
		goto readyend;
	flashstaple:
		TNT1 A 1{
			A_PlaySound("medikit/staple",CHAN_WEAPON);
			A_PlaySound("misc/bulletflesh",CHAN_BODY);
			invoker.patchwound(1,self);
		}goto flashend;
	flashnail:
		TNT1 A 1{
			A_PlaySound("medikit/stopper",CHAN_WEAPON);
			A_PlaySound("misc/bulletflesh",CHAN_BODY);
			invoker.patchwound(random(1,3),self);
		}goto flashend;
	flashend:
		TNT1 A 1{
			givebody(1);
			damagemobj(invoker,self,1,"staples");
			A_ZoomRecoil(0.9);
			A_ChangeVelocity(frandom(-1,0.1),frandom(-1,1),0.5,CVF_RELATIVE);
		}
		stop;
	altfire:
	althold:
	fireother:
		TNT1 A 0 A_JumpIf(pressingfiremode()&&!pressingzoom(),"diagnoseother");
		TNT1 A 10{
			flinetracedata mediline;
			linetrace(
				angle,42,pitch,
				offsetz:height-12,
				data:mediline
			);
			let c=HDPlayerPawn(mediline.hitactor);
			if(!c){
				if(getcvar("hd_helptext"))A_WeaponMessage("Nothing to be done here.\n\nHeal thyself? (press fire)",150);
				return resolvestate("nope");
			}
			if(invoker.usedon&&c!=invoker.usedon){
				if(c.getcvar("hd_helptext"))c.A_Print(string.format("Get the hell away!\n\n%s is trying to stab you\n\nwith a used syringe!!!",player.getusername()));
				if(getcvar("hd_helptext"))A_Print("Why are you attacking your teammate\n\nwith used medical equipment!?");
			}else if(c.countinv("IsMoving")>4){
				if(c.getcvar("hd_helptext"))c.A_Print(string.format("Stop squirming!\n\n%s is trying to heal you\n\nnot bugger you...",player.getusername()));
				if(getcvar("hd_helptext"))A_WeaponMessage("You'll need them to stay still...");
				return resolvestate("nope");
			}
			if(!c.player.bot && c.countinv("WornRadsuit") && c.countinv("PortableRadsuit")){
				if(getcvar("hd_helptext"))A_WeaponMessage("Get them to take off their environment suit first!\n\n(toggle it in the inventory)",100);
				return resolvestate("nope");
			}
			if(!c.player.bot && c.countinv("HDArmourWorn")){
				if(getcvar("hd_helptext"))A_WeaponMessage("Get them to take off their armour first!\n\n(\cdhd_strip\c- in the console)",100);
				return resolvestate("nope");
			}
			if(!(getplayerinput(MODINPUT_BUTTONS)&BT_ALTATTACK) && c.woundcount+c.unstablewoundcount<1){
				A_WeaponMessage("They're not bleeding.");
				return resolvestate("nope");
			}
			if(getplayerinput(MODINPUT_BUTTONS)&BT_ALTATTACK && c.burncount+c.oldwoundcount<1){
				A_WeaponMessage("They have no lasting injuries to treat.");
				return resolvestate("nope");
			}
			if(getplayerinput(MODINPUT_BUTTONS)&BT_ALTATTACK && invoker.injectableflesh<1){
				A_WeaponMessage("You have no Second Flesh to apply.");
				return resolvestate("nope");
			}
			invoker.target=c;
			return resolvestate("applythatshit");
		}goto nope;
	applythatshit:
		TNT1 A 0{
			if(invoker.target)invoker.usedon=invoker.target;
		}
		TNT1 A 0 A_JumpIf(pressingzoom()&&pressingfiremode(),"applythathotshit");
		TNT1 A 0{invoker.usedpatches++;}
		TNT1 A 0 A_Jump(112,"applythatstaplershit");
	applythatinjectorshit:
		TNT1 A 10{
			if(invoker.target){
				invoker.target.A_PlaySound("medikit/stopper",CHAN_WEAPON);
				invoker.target.A_PlaySound("misc/bulletflesh",CHAN_BODY);
				invoker.patchwound(random(1,3),invoker.target);
			}
		}goto patchupend;
	applythatstaplershit:
		TNT1 AAAAA 3{
			A_PlaySound("medikit/staple",CHAN_WEAPON);
			if(invoker.target){
				invoker.target.A_PlaySound("misc/smallslop",CHAN_BODY);
				invoker.patchwound(1,invoker.target);
				if(!random(0,3))invoker.setstatelabel("patchupend");
				invoker.target.givebody(1);
				invoker.target.damagemobj(invoker,null,1,"staples",DMG_FORCED);
			}
		}goto patchupend;
	applythathotshit:
		TNT1 A 10{
			if(invoker.target){
				invoker.injectableflesh--;
				invoker.target.A_PlaySound("medikit/stopper",CHAN_WEAPON);
				invoker.target.A_PlaySound("misc/bulletflesh",CHAN_BODY);
				invoker.target.A_PlaySound("misc/smallslop",CHAN_VOICE);
				actor a=spawn("SecondFleshBeast",invoker.target.pos,ALLOW_REPLACE);
				a.target=invoker.target;
				a.stamina=MEDIKIT_FLESHGIVE;
			}
		}
		goto nope;
	patchup:
		TNT1 A 10;
		TNT1 A 0{
			if(!invoker.usedon)invoker.usedon=self;
			invoker.usedpatches++;
		}
		TNT1 A 0 A_Jump(112,"stapler");
	injector:
		TNT1 A 0 A_Overlay(3,"flashnail");
		goto patchupend;
	stapler:
		TNT1 AAAAA random(4,5){
			A_Overlay(3,"flashstaple");
			if(!random(0,3))invoker.setstatelabel("patchupend");
		}goto patchupend;
	patchupend:
		TNT1 A 10;
		TNT1 A 0 A_ClearRefire();
		goto ready;
	secondflesh:
		TNT1 A 10;
		TNT1 A 10{
			if(!(self is "HDPlayerPawn"))return;
			invoker.usedon=self;
			invoker.injectableflesh--;
			A_PlaySound("medikit/stopper",CHAN_WEAPON);
			A_PlaySound("misc/bulletflesh",CHAN_BODY);
			A_PlaySound("misc/smallslop",CHAN_VOICE);
			actor a=spawn("SecondFleshBeast",pos,ALLOW_REPLACE);
			a.target=self;
			a.stamina=MEDIKIT_FLESHGIVE;
		}
		goto nope;

	diagnose:
		TNT1 A 0 A_WeaponMessage("\cdMedikit Auto-Diagnostic Tool engaged.\c-\n\n\ccScanning, please wait...");
		TNT1 AAAAAAAAAAAA 2{
			A_PlaySound("medikit/scan",CHAN_WEAPON,0.4);
			A_SetBlend("aa aa 88",0.04,1);
		}
		TNT1 A 0 A_ScanResults(self,12);
		TNT1 A 0 A_Refire("nope");
		goto readyend;
	diagnoseother:
		TNT1 A 0{
			A_WeaponMessage("\cdMedikit Auto-Diagnostic Tool engaged.\c-\n\n\ccScanning, please wait...");
			invoker.scantarget=null;
			invoker.scanaccuracy=0;
		}
		TNT1 AAAAAAAAAAAA 2{
			flinetracedata mediline;
			linetrace(
				angle,42,pitch,
				offsetz:height-12,
				data:mediline
			);
			let mha=mediline.hitactor;
			if(
				!mha
				||(invoker.scantarget&&mha!=invoker.scantarget)
			){
				invoker.scantarget=null;
				invoker.scanaccuracy=0;
				return;
			}
			invoker.scantarget=mha;
			invoker.scanaccuracy++;
		}
		TNT1 A 0 A_ScanResults(invoker.scantarget,invoker.scanaccuracy);
		TNT1 A 0 A_Refire("nope");
		goto readyend;

	spawn:
		MEDI B -1 nodelay{
			if(invoker.usedon){
				frame=2;
				actor bbb=spawn("BloodSplatSilent",pos,ALLOW_REPLACE);
				if(bbb)bbb.vel=vel;
				tics=random(10,600);
			}
		}wait;
	}
	action void A_ScanResults(actor scanactor,int scanaccuracy){
			A_PlaySound("medikit/done",CHAN_WEAPON);
			int thrownoff=scanaccuracy-12;
			if(!scanactor||abs(thrownoff)>10){
				A_WeaponMessage("\caMedikit Auto-Diagnostic Tool failed.");
				invoker.scantarget=null;
				invoker.scanaccuracy=0;
				return;
			}
			string scanactorname=scanactor.getclassname();
			if(scanactor.player)scanactorname=scanactor.player.getusername();
			let slf=HDPlayerPawn(scanactor);
			if(!slf){
				int scanactorhealthpercent=scanactor.health*100/scanactor.spawnhealth();
				A_WeaponMessage(string.format("Medikit Auto-Diagnostic complete.

				Status report:

				\ccPatient: \cy%s

				\ccOverall Health: \cg%u%%

				\cu(all numbers are based on %% of minimum
				\cuconsidered to be lethal in all situations.)",
				scanactorname,scanactorhealthpercent+random(-thrownoff,thrownoff)),210);

				A_Log(string.format("Medikit Auto-Diagnostic:
\ccPatient: \cy%s
\ccOverall Health: \cg%u%%",scanactorname,scanactorhealthpercent),true);
				return;
			}
			int uw=slf.unstablewoundcount;
			int ww=slf.woundcount;
			int ow=slf.oldwoundcount;
			int bb=slf.burncount;
			int ag=slf.aggravateddamage*0.2+countinv("IsMoving")+abs(thrownoff);
			int wg=random(-thrownoff,thrownoff);
			if(ww||uw)wg+=2;
			if(countinv("HDArmourWorn"))wg+=5;
			ow=max(ow+random(-ag,ag),0);
			bb=max(bb+random(-ag,ag),0);
			uw=max(uw+random(-wg,wg),0);
			ww=max(ww+random(-wg,wg),0);
			A_WeaponMessage(string.format("Medikit Auto-Diagnostic complete.

			Status report:

			\ccPatient: \cy%s

			\ccOpen wounds: \cg%u%%

			\ccWounds temporarily bandaged: \ca%u%%
			\ccWounds already treated: \cd%u%%
			\ccBurns: \cq%u%%

			\cu(all numbers are based on %% of minimum
			\cuconsidered to be lethal in all situations.)",
			scanactorname,ww,uw,ow,bb),210);

			A_Log(string.format("Medikit Auto-Diagnostic:
\ccPatient: \cy%s
\ccOpen wounds: \cg%u%%
\ccWounds temporarily bandaged: \ca%u%%
\ccWounds already treated: \cd%u%%
\ccBurns: \cq%u%%",scanactorname,ww,uw,ow,bb),true);
	}
	override string pickupmessage(){
		if(injectableflesh<MEDIKIT_MAXFLESH||usedpatches>0)return "Picked up a used medikit.";
		return "Picked up an opened medikit.";
	}
}
class SecondFleshBeast:IdleDummy{
	states{
	spawn:
		TNT1 A 30;
		TNT1 A 12{target.A_Scream();}
		TNT1 A 4{
			let tgt=HDPlayerPawn(target);
			if(!tgt || tgt.bkilled || stamina<1){destroy();return;}
			if(tgt.health>10)tgt.damagemobj(tgt,tgt,min(tgt.health-10,3),"internal",DMG_NO_ARMOR);
			tics=clamp(200-stamina,4,random(4,40));
			if(tics<10)tgt.A_Pain();
			tgt.stunned+=10;
			switch(random(0,tgt.incapacitated?10:21)){
			case 0:
			case 1:
				tgt.burncount--;
				break;
			case 2:
			case 3:
				tgt.oldwoundcount--;
				break;
			case 4:
				if(tgt.unstablewoundcount){
					tgt.unstablewoundcount--;
					tgt.aggravateddamage++;
				}
			default:break;
			}
			if(!random(0,200))tgt.aggravateddamage++;
			stamina--;
			if(hd_debug)A_Log(string.format("aggro %i  old %i  unstable %i",tgt.aggravateddamage,tgt.oldwoundcount,tgt.unstablewoundcount));
		}wait;
	}
}


class SelfBandage:HDWoundFixer{
	default{
		weapon.selectionorder 1004;
		weapon.slotnumber 9;
	}
	void patchwound(int amt,actor targ){
		let slf=HDPlayerPawn(targ);
		if(!slf)return;
		if(!random(0,2)&&(slf.alpha<1||slf.bshadow))amt-=random(0,amt+1);
		int wound=max(slf.woundcount,0);
		amt=min(amt,wound);
		if(wound){
			amt=min(amt,wound);
			slf.woundcount-=amt;
			slf.unstablewoundcount+=amt;
		}
	}
	override string,double getpickupsprite(){return "BLUDC0",1.;}
	override string gethelptext(){return "\cuImprovised Bandaging\n"..WEPHELP_INJECTOR
		..(owner.countinv("BloodBagWorn")?"\n"..WEPHELP_ALTRELOAD.."  Remove blood feeder":"");}
	override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		int of=0;
		if(hpl.woundcount){
			sb.drawimage(
				"BLUDC0",(-17,-6),
				sb.DI_SCREEN_CENTER_BOTTOM|sb.DI_ITEM_RIGHT,
				0.6
			);
			of=clamp(hpl.woundcount*0.2,1,3);
			if(hpl.flip)of=-of;
		}
		sb.drawwepdot(-22,-8+of,(2,10));
		sb.drawwepdot(-17,-12+of,(12,2));
	}
	override inventory CreateTossable(int amount){
		DropMeds(owner,0);
		return null;
	}
	int targetlock;
	states{
	select:
		TNT1 A 0{
			if(!getcvar("hd_helptext")) return;
			if(invoker.checkwoundcount())A_WeaponMessage("\cu--- \ccBANDAGING \cu---\c-\n\n\nPress and hold Fire\n\nwhile standing still\n\nto try to not die.",210);
			else A_WeaponMessage("\cu--- \ccBANDAGING \cu---\c-\n\n\nPress and hold Fire to bandage\n\nyourself when you are bleeding.\n\n\n\nPress and hold Altfire\n\nto bandage someone else.",210);
		}
		goto super::select;
	abort:
		TNT1 A 1{
			if(getcvar("hd_helptext"))A_WeaponMessage("You must stay still\n\nto bandage yourself!",70);
		}
		TNT1 A 0 A_Refire("lower");
		goto nope;
	fire:
		TNT1 A 0{
			bool nope=false;
			if(countinv("PortableRadsuit") && countinv("WornRadsuit")){
				if(getcvar("hd_helptext"))A_WeaponMessage("Take off your environment suit first!",70);
				nope=true;
			}
			else if(!invoker.checkwoundcount()){
				if(getcvar("hd_helptext"))A_WeaponMessage("You are not bleeding.",70);
				nope=true;
			}
			if(nope)player.setpsprite(PSP_WEAPON,invoker.findstate("nope"));
		}
	hold:
	lower:
		TNT1 A 0 A_JumpIf(pitch<45,1);
		goto try;
		TNT1 A 1 A_SetPitch(max(90,pitch+6),SPF_INTERPOLATE);
		TNT1 A 0 A_JumpIfInventory("IsMoving",4,"abort");
		TNT1 A 0 A_Refire("lower");
		goto ready;
	try:
		TNT1 A random(15,25);
		TNT1 A 0{
			A_MuzzleClimb(frandom(-1.5,1.8),frandom(-2.4,2.4));
			if(hdplayerpawn(self))hdplayerpawn(self).fatigue+=2;
		}
		TNT1 A 0 A_Jump(32,2);
		TNT1 A random(5,15) damagemobj(self,self,1,"bleedout");
		TNT1 A 0 A_JumpIfInventory("IsMoving",4,"abort");
	try2:
		TNT1 A 0{
			A_MuzzleClimb(frandom(-1.5,1.8),frandom(-2.4,2.4));
			if(hdplayerpawn(self))hdplayerpawn(self).fatigue+=2;
		}
		TNT1 A random(1,3) A_Jump(32,2,4);
		TNT1 A 0 A_Jump(256,2);
		TNT1 A random(1,3) A_PlaySound("*usefail",CHAN_VOICE);
		TNT1 A 0 A_Jump(256,2);
		TNT1 A random(1,3) A_PlaySound("*grunt",CHAN_VOICE);
		TNT1 A 0 A_Jump(200,2);
		TNT1 A 0 A_PlaySound("bandage/rip",CHAN_WEAPON,0.4);
		TNT1 A 0 A_Refire("try4");
		goto ready;
	try3:
		TNT1 A random(20,40){
			A_MuzzleClimb(frandom(-1.6,1.8),frandom(-2.4,2.4));
			if(hdplayerpawn(self))hdplayerpawn(self).fatigue+=2;
		}
		TNT1 A 0 A_Jump(200,2);
		TNT1 A 0 A_PlaySound("bandage/rustle");
		TNT1 A random(10,20);
		TNT1 A 0 A_JumpIfInventory("IsMoving",4,"abort");
		TNT1 A 0 A_Refire("try4");
		goto ready;
	try4:
		TNT1 A 0 A_CheckFloor(2);
		TNT1 A 0 A_Jump(240,2);
		TNT1 A 0 A_ChangeVelocity(frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-2,2));
		TNT1 A 0{
			A_MuzzleClimb(frandom(-1.5,1.7),frandom(-2.4,2.4));
			if(hdplayerpawn(self))hdplayerpawn(self).fatigue+=2;
		}
		TNT1 A 0 A_Jump(240,2);
		TNT1 A random(1,3) A_PlaySound("*grunt",CHAN_VOICE);
		TNT1 A 0 A_Jump(140,2);
		TNT1 A 0 A_PlaySound("bandage/rustle");
		TNT1 A random(10,20);
		TNT1 A 0 A_JumpIfInventory("IsMoving",4,"abort");
		TNT1 A 0 A_Refire("try5");
		goto ready;
	try5:
		TNT1 A 0 A_MuzzleClimb(frandom(-1.8,1.8),frandom(-2.4,2.4));
		TNT1 A 0 A_Jump(8,"try2");
		TNT1 A 0 A_Jump(12,"try3");
		TNT1 A 0 A_Jump(16,"try4");
		TNT1 A 0 A_Jump(80,2);
		TNT1 A 0 A_PlaySound("bandage/rustle");
		TNT1 A random(10,20);
		TNT1 A 0 A_Jump(80,2);
		TNT1 A 0 A_PlaySound("weapons/pocket");
		TNT1 A random(10,20);
		TNT1 A 0 A_JumpIfInventory("IsMoving",4,"abort");
		TNT1 A 0 A_JumpIf(invoker.checkwoundcount(),2);
		TNT1 A 0 {
			if(getcvar("hd_helptext"))A_WeaponMessage("You seem to be stable.",144);
		}goto nope;
		TNT1 A 0 A_Jump(42,2);
		TNT1 A 0 A_JumpIfInventory("HDArmourWorn",1,2);
		TNT1 A 4 A_Jump(100,2,3);
		TNT1 A 0 {invoker.patchwound(random(1,3),self);}
		TNT1 A 0 A_MuzzleClimb(frandom(-2.4,2.4),frandom(-2.4,2.4));
		TNT1 A 0 A_Refire("try2");
		goto ready;
	nope:
		TNT1 A 0{invoker.targetlock=0;}
		goto super::nope;
	altfire:
	althold:
		TNT1 A 1;
		TNT1 A 0{
			actor a;int b;
			[a,b]=LineAttack(angle,42,pitch,0,"none",
				"NewBulletMovePuff",flags:LAF_NORANDOMPUFFZ|LAF_NOINTERACT
			);
			let c=HDPlayerPawn(a.tracer);
			if(!c){
				A_WeaponMessage("Nothing to be done here.\n\nHeal thyself?");
				return resolvestate("nope");
			}
			if(c.countinv("IsMoving")>4){
				c.A_Print(string.format("Stop squirming!\n\n%s is trying to bandage you\n\nnot bugger you...",player.getusername()));
				A_WeaponMessage("You'll need them to stay still...");
				return resolvestate("nope");
			}
			if(c.woundcount<1){
				A_WeaponMessage("They're not bleeding.");
				return resolvestate("nope");
			}
			invoker.target=c;
			invoker.targetlock++;
			if(invoker.targetlock>10){
				A_Refire("injectbandage");
			}else A_Refire();
			return resolvestate(null);
		}goto nope;
	injectbandage:
		TNT1 A random(7,14){
			if(invoker.target){
				if(random(0,2)){
					if(!random(0,2))invoker.target.A_PlaySound("bandage/rustle",CHAN_BODY);
					return;
				}
				invoker.target.A_PlaySound("weapons/pocket",CHAN_BODY);
				invoker.patchwound(random(3,5),invoker.target);
			}
		}goto ready;

	altreload:
		TNT1 A 0 A_PlaySound("weapons/pocket",CHAN_BODY);
		TNT1 A 15 A_JumpIf(!countinv("BloodBagWorn")||countinv("WornRadsuit"),"nope");
		TNT1 A 10{
			A_SetBlend("7a 3a 18",0.1,4);
			A_SetPitch(pitch+2,SPF_INTERPOLATE);
			A_PlaySound("*usemeds",CHAN_VOICE);
			A_DropInventory("BloodBagWorn");
		}
		goto nope;


	spawn:
		TNT1 A 1;
		stop;
	}
}



